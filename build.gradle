buildscript {
    repositories {
        jcenter()
        maven {url "http://files.minecraftforge.net/maven"}
        maven {url "https://plugins.gradle.org/m2/"}
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

String ttag = System.getenv("TRAVIS_TAG")
version = ttag ? ttag : "0.0.0-x"
group = "szewek.fl"
archivesBaseName = "fl"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

task sourcesJar(type: Jar, dependsOn: "sourceJar") {
    from sourceSets.main.allJava
    from sourceSets.api.allJava
    classifier = "sources"
}

minecraft {
    version = "1.11.2-13.20.0.2252"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20170212"
    replace "@VERSION@", project.version
    replaceIn "R.java"
}

publishing {
    publications {
        FLPub(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["FLPub"]
    publish = true
    override = false
    pkg {
        repo = "MC"
        name = project.group
        version {
            name = project.version
        }
    }
}

dependencies {

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
curseforge {
    String cfkey = System.getenv("CURSEFORGE_KEY")
    apiKey = cfkey ? cfkey : ""
    project {
        id = "262385"
        releaseType = "release"
        changelogType = "markdown"
        changelog = file("LATEST.md")
    }
}

artifacts {
    archives sourcesJar
}